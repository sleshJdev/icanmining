buildscript {
    ext {
        springBootVersion = '1.5.9.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'

group = 'by.miner'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile('org.springframework.boot:spring-boot-starter-data-jpa')
    compile('org.springframework.boot:spring-boot-starter-security')
    compile('org.springframework.boot:spring-boot-starter-web')
    compile('mysql:mysql-connector-java:5.1.44')
    compile('com.zaxxer:HikariCP:2.7.8')
    compile('com.auth0:java-jwt:3.1.0')
    compile('org.flywaydb:flyway-core:5.0.7')
    testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile('org.springframework.security:spring-security-test')
    testCompile('org.flywaydb.flyway-test-extensions:flyway-spring-test:5.0.0')
}

task buildClient() {
    doLast {
        exec {
            workingDir './client'
            executable 'yarn'
            args 'run', 'build'
        }
        delete './src/main/webapp'
        copy {
            from './client/dist'
            into './src/main/webapp'
        }
    }
}

tasks.war.dependsOn buildClient

task dockerBuild(type: Exec, dependsOn: war) {
    def appFile = rootDir.toURI().relativize(war.archivePath.toURI())
    //actually, the current configuration works only for 'prod' profile
    //todo: investigate how fix the configuration
    def profile = getProperty('profile') ?: 'dev';
    println("profile: ${profile}, appFile: ${appFile}")
    executable 'docker-compose'
    args 'build'
    args '--build-arg', "app_file=${appFile}"
    args '--build-arg', "app_profile=${profile}"
}

task dockerUp(type: Exec, dependsOn: dockerBuild) {
    executable 'docker-compose'
    args 'up'
}